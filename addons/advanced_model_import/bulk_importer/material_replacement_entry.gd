@tool
class_name MaterialReplacementEntry
extends HBoxContainer

signal path_changed(entry: MaterialReplacementEntry)
signal remove_requested(entry: MaterialReplacementEntry)

@export var _mat_name_edit: LineEdit = null
@export var _mat_path_edit: LineEdit = null
@export var _set_path_button: Button = null
@export var _remove_button: Button = null

var material_name: String:
	get:
		return _material_name_value
	set(value):
		_material_name_value = value
		if is_instance_valid(_mat_name_edit):
			_mat_name_edit.text = value
			_mat_name_edit.tooltip_text = value
var material_path: String:
	get:
		return _material_path_value
	set(value):
		_material_path_value = value
		if is_instance_valid(_mat_path_edit):
			_mat_path_edit.text = value
			_mat_path_edit.tooltip_text = value

var _material_name_value: String = ""
var _material_path_value: String = ""
var _material_file_dialog: EditorFileDialog = null


func _ready() -> void:
	_mat_name_edit.text = material_name
	_mat_path_edit.text = material_path


func _enter_tree() -> void:
	_set_path_button.icon = BulkImporterDock.load_icon
	_remove_button.icon = BulkImporterDock.remove_icon

	_set_path_button.pressed.connect(_on_set_path_pressed)
	_remove_button.pressed.connect(_on_remove_pressed)


func _exit_tree() -> void:
	_set_path_button.pressed.disconnect(_on_set_path_pressed)
	_remove_button.pressed.disconnect(_on_remove_pressed)

	if is_instance_valid(_material_file_dialog):
		var callback := _on_material_replace_file_selected.bind(self)
		if _material_file_dialog.file_selected.is_connected(callback):
			_material_file_dialog.file_selected.disconnect(callback)


#region Utilities

func setup(material_file_dialog: EditorFileDialog) -> void:
	_material_file_dialog = material_file_dialog

#endregion

#region Signals & Callbacks

func _on_set_path_pressed() -> void:
	if !is_instance_valid(_material_file_dialog):
		return

	var callback := _on_material_replace_file_selected.bind(self)
	if !_material_file_dialog.file_selected.is_connected(callback):
		_material_file_dialog.file_selected.connect(callback, CONNECT_ONE_SHOT)

	_material_file_dialog.current_path = material_path
	_material_file_dialog.popup_centered()


func _on_remove_pressed() -> void:
	remove_requested.emit(self)


func _on_material_replace_file_selected(path: String, entry: MaterialReplacementEntry) -> void:
	if entry != self:
		return

	material_path = path
	path_changed.emit(self)

#endregion
